/com,COLLABORATIVE SOLUTIONS, INC. (CSI) DOES NOT GUARANTEE THE CORRECTNESS
/com,OR USEFULNESS OF THE RESULTS OBTAINED USING THE EXAMPLE. CSI IS NOT
/com,LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/com,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/com,USEFULNESS OF THE RESULTS.
/com,
!com,AUTHOR:
!com,David Haberman
!com,Collaborative Solutions, Inc.
!com,5962 La Place Court Suite 100
!com,Carlsbad CA 92009
!com,760-431-4815
!com,david.haberman@csi-cae.com
/com,
/com,DESCRIPTION 
/com,Structural Transient
/com,Pre-Stressed Modal Superposition
/com,Large Mass Method
/com,Acceleration vs. Time  Acceleration:f=ma
/com,

finish
/clear
!!!!Example of transient modal superposition!!!!!!!!!!!!
!!!!Large mass method to apply acceleration vs. time!!!!
!!!!With Pre-stress!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

!!!!Build Model!!!!
/PREP7  
RECTNG,10,,1,,  
ET,1,SHELL181   
KEYOPT,1,3,2  ! full intergration
R,1,.1, , , , , ,  
RMORE, , , , , , ,  
/MPLIB,READ,d:\apps\ansys56\matlib  
/MPLIB,WRITE,    
MAT,1,  
! /INPUT,d:\apps\ansys56\matlib\Stl_AISI-304.BIN_MPL
! /COM,Typical material properties for DEMO purposes only 
! /COM,ANSYS REVISION  5.3    UP042296 TEST    16:35:50    04/23/1996 
/NOP
! /COM,Internal UNITS set at file creation time = BIN 
TBDEL,ALL,_MATL 
MPDEL,ALL,_MATL 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,EX  ,_MATL , 1,  27992720.0    , 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,NUXY,_MATL , 1, 0.290000000    , 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,ALPX,_MATL , 1, 9.888888889E-06, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,DENS,_MATL , 1, 7.514795200E-04, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,KXX ,_MATL , 1, 2.182244000E-04, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,C   ,_MATL , 1,  46.2864080    , 
!
esize,.5   
amesh,all   
numoff,node,100

!!!!Add large mass!!!
ET,2,MASS21 
KEYOPT,2,2,0
KEYOPT,2,3,0
R,2,1e5,1e5,1e5,1e5,1e5,1e5, 
n,1,-1   
type,2  
real,2  
mat,2   
e,1
nsel,s,loc,x,-1,0   
cerig,1,all,all
numcmp,all   
FINISH  

!!!!Pre-Stress Static!!!!
allsel,all  
/solu

ANTYPE,0
D,1, , , , , ,ALL, , , , ,   
F,3,FX,10
F,23,FX,10
F,24,FX,10

PSTRES,ON  
solve
finish

!!!!Modal Analysis!!!!
/SOLU   
ANTYPE,2
MXPAND,10, , ,1 
MODOPT,lanb,10,1,0, ,OFF, ,0
allsel,all

ddele,1,all
F,3,FX,0
F,23,FX,0 
F,24,FX,0 

PSTRES,1
SOLVE   
!
FINISH  

/POST1  
set,1,1
plns,u,sum
FINISH  

!!!!Transient Analysis!!!!
/SOLU   
!*  
!!!!1st load step at time 0.0 sec!!!!
ANTYPE,4
TRNOPT,MSUP,10
DMPRAT,.03, 
deltim,.01/4    ! ITS
allsel,all
solve
!!!!2nd load step time=.01 sec at the end of LS!!!!
TIME,.01  
KBC,0   
outres,all,all
nsel,s,loc,x,-1,-1
F,all,fz,386.4*1e5                     ! 386.6*1e5
allsel,all
solve
!!!!3rd load step time=.02 sec at the end of LS!!!!
time,.02
nsel,s,loc,x,-1,-1
f,all,fz,0
allsel,all
solve
!!!!4th load step time=.25 sec at the end of LS!!!!
time,.25
allsel,all
solve
!

!!!!Postprocess RDSP File!!!!
/POST26 
NUMVAR,10,  
FILE,,rdsp,
NSOL,2,57,U,z,  
!*  
PRVAR,2, , , , , ,  
plvar,2

/eof

!
FINISH  

!!!!Expand results (RDSP file) Note this will overwrite
!!!!the mode shapes on the RST file!!!!
/SOLU   
EXPASS,1
NUMEXP,25*4,0,.25
!

!!!!Postprocess expanded results one time point!!!!
SOLVE   
FINISH  
/POST1  
SET,FIRST   
PLNSOL,U,Z,0,1  


!!!!Postprocess expanded results x-y plots of response vs. time!!!!
FINISH  
/POST26 
NSOL,2,23,U,Z,  
!*  
PLVAR,2, , , , , , , , , ,  
!
!


