/com,COLLABORATIVE SOLUTIONS, INC. (CSI) DOES NOT GUARANTEE THE CORRECTNESS
/com,OR USEFULNESS OF THE RESULTS OBTAINED USING THE EXAMPLE. CSI IS NOT
/com,LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/com,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/com,USEFULNESS OF THE RESULTS.
/com,
!com,AUTHOR:
!com,David Haberman
!com,Collaborative Solutions, Inc.
!com,5962 La Place Court Suite 100
!com,Carlsbad CA 92009
!com,760-431-4815
!com,david.haberman@csi-cae.com
/com,
/com,DESCRIPTION 
/com,Harmonic linear Sweep Example. Modal super position with the large mass
/com,method was the solution technique. The cluster option was used to allow
/com,for enough resolution around the natural freq.
/com,

finish
/clear
/PREP7  

xm=1e5       ! mass for the large mass
g=386.4      ! acceration of gravity
totg=1       ! total number of g's applied to the model
xa=g*totg    ! acceleration applied to the model
xf=xm*xa     ! force required to applied desired acceleration f=ma

pi=3.141592654

/title,LARGE MASS METHOD ACCELERATION INPUT

RECTNG,20,,.25,, 
ET,1,PLANE42
UIMP,1,EX, , ,30e6, 
UIMP,1,NUXY, , ,.3, 
UIMP,1,DENS, , ,.000729,

LESIZE,2, , ,4, , , , ,1 
LESIZE,4, , ,4, , , , ,1  

LESIZE,1, , ,80, , , , ,1 
LESIZE,3, , ,80, , , , ,1 

MSHAPE,0,2D 
MSHKEY,1

AMESH,all   
numoff,node,100

!!!!Add large mass!!!
ET,2,MASS21 
KEYOPT,2,2,0
KEYOPT,2,3,0
R,2,1e5,1e5,1e5,1e5,1e5,1e5, 
n,1,-1   
type,2  
real,2  
mat,2   
e,1
nsel,s,loc,x,-1,0   
cerig,1,all,all,uxyz
allsel,all
numcmp,all 
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
FINISH  
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!MODAL ANALYSIS!!!!!!!!!!!!!!!!!!!!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
/SOLU   
ANTYPE,2
EQSLV,SPAR  
MXPAND,20, , ,0  
MODOPT,LANB,20,0,0, ,OFF, ,0   ! no shift get all rigid body modes
! this will allow the acceleration at the large mass to be calculated
! otherwise the acceleration at the large mass is incorrect
SOLVE   
FINISH  
/POST1  

finish
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!HARMONIC ANALYSIS!!!!!!!!!!!!!!!!!!!!!
/solu

ANTYPE,3
!*  
HROPT,MSUP,0, , 
HROUT,ON,OFF,0  EQSLV,FRONT,0,  

KBC,1   
DMPRAT,.03, 
outres,all,all  

f,1,fy,(xf)
harfrq,10,40

hrout,,on        ! turns cluster option on, ansys calculated freqs
nsubst,10        ! 1-10 default 4

solve
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
finish
!!!!POSTPROCESS TIME HISTORY!!!!!!!!!!!!!!!!!!!!!!
/POST26 
file,,rfrq

NSOL,2,83,U,Y,                     !  node 83 displacement

PROD,4,1,1, , , , ,2*pi,2*pi,1,    ! omega squared
PROD,5,2,4, , , , ,1,1,1,          ! acceleration
PROD,6,5, , , , , ,1/g             ! results in g's

/AXLAB,X,Freqency   
/AXLAB,Y,Response in G  
/GROPT,LOGX,ON  
/GROPT,LOGY,OFF 

PLVAR,6

/eof

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!!!Expansion results to .rst file!!!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
FINISH  
/SOLU   
EXPASS,1
NUMEXP,25,10,40,1  
HREXP,ALL,  
!*  
/STATUS,SOLU
SOLVE   
FINISH  


!!!!POSTPROCESS A SINGLE FREQUENCY!!!!
/post1
 



