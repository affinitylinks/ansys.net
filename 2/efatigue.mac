*GET,_MENU,ACTIVE,,MENU
*IF,_MENU,EQ,1,THEN
*GET,_LEVEL,ACTIVE,,ROUT
*IF,_LEVEL,NE,31,THEN
/POST1
*ENDIF
*GET,_MAXN,NODE,,NUM,MAX

MULTIPRO,'START',1
*CSET,1,3,_LOADS,'ENTER THE NUMBER OF LOADINGS'
*CSET,61,62,'NUMBER OF LOAD STEPS',' USED IN THE ANALYSIS'
MULTIPRO,'END'

!_EPL = EVENTS PER LOAD STEP
_INC=0
MULTIPRO,'START',_LOADS
*DO,I,1,_LOADS,1
*CSET,1+_INC,3+_INC,_EPL%I%,'# OF EVENTS FOR LOAD STEP %I%'
_INC=_INC+3
*ENDDO
*CSET,61,62,'EVENTS PER LOAD STEP'
MULTIPRO,'END'

!TOTAL NUMBER OF EVENTS
_EVENTS=0
*DO,I,1,_LOADS,1
_EVENTS = _EVENTS+_EPL%I%
*ENDDO

FTSIZE,_MAXN,_EVENTS,_LOADS

_INC=0
MULTIPRO,'START',_EVENTS
*DO,I,1,_LOADS,1
*DO,J,1,_EPL%I%,1
*CSET,1+_INC,3+_INC,_NCYCLE%I%%J%,'CYCLES FOR LOAD%I% EVENT %J%'
_INC=_INC+3
*ENDDO
*ENDDO
MULTIPRO,'END'

_INC=0
MULTIPRO,'START',_EVENTS
*DO,I,1,_LOADS,1
*DO,J,1,_EPL%I%,1
*CSET,1+_INC,3+_INC,_SCALE%I%%J%,'_SCALE FACTOR FOR LOAD%I% EVENT %J%'
_INC=_INC+3
*ENDDO
*ENDDO
MULTIPRO,'END'

MULTIPRO,'start',1
*CSET,1,3,_CHK,'DO YOU WANT TO CREATE S-N DATA (1/0)',0
*CSET,61,62,'INPUT THE DATA IN THE VI EDITOR AND SAVE THE FILE'
*CSET,63,64,'DO NOT RENAME THE FILE NAME'
MULTIPRO,'END'
*if,_CHK,EQ,0,THEN
/INPUT,fatigue,dat
*ELSE
/SYS,cat /home/narayan/sample.fatg > fatigue.dat
/SYS,vi fatigue.dat
/INPUT,fatigue,dat
*ENDIF

_TMP=1
*ABSET,'Storing Nodal Stresses..',BOTH
_INC=1
*DO,I,1,_LOADS,1
SET,I
*DO,J,1,_EPL%I%,1
*DO,K,1,_MAXN,1
FSNODE,K,_INC,I
*ABCHECK,NINT(_TMP*100/(_MAXN*0.5*_EVENTS*_LOADS)),'NODE %K% EVENT %_INC% LOAD %I%'
_TMP = _TMP+1
*ENDDO
_INC=_INC+1
*ENDDO
*ENDDO
*ABFINISH

_INC=1
*DO,I,1,_LOADS,1
*DO,J,1,_EPL%I%,1
FE,_INC,_NCYCLE%I%%J%,_SCALE%I%%J%,LOAD %I% EVENT %J%
_INC=_INC+1
*ENDDO
*ENDDO


! CALCULATE FATIGUE
*ABSET,'Have Patience..',BOTH
/OUTPUT,out,fatg
*DO,I,1,_MAXN,1
*ABCHECK,NINT(I*100/_MAXN),'PROCESSED %I% OF %_MAXN% NODES'
FTCALC,,I
*ENDDO
*ABFINISH
/OUTPUT
/SYS,grep CUMULATIVE out.fatg > temp.fatg
/SYS,rm -f out.fatg
/SYS,cat temp.fatg | awk '{print $5}' > damage.fatg
/SYS,rm -f temp.fatg
*DIM,DARRAY,ARRAY,_MAXN,1
*VREAD,DARRAY(1),damage,fatg
(F10.4)
*DO,I,1,_MAXN,1
DNSOL,I,U,X,DARRAY(I)
*ENDDO
/SYS,rm -f damage.fatg
/TITLE,CONTOUR PLOT OF CUMULATIVE DAMAGE ACCUMULATION
/VIEW,1,1,1,1
PLNSOL,U,X
*ELSE
*MSG,WARN
YOU HAVE TO BE IN THE GUI TO USE THE MACRO
*ENDIF
