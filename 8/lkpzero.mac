! ANSYS REVISION 5.6, LKPZERO.MAC
!
!     DELETES LINES HAVING KEYPOINTS 0 0 WHICH MAY HAVE BEEN
!        CREATED BY FAILED AMESH OR VMESH AT ANSYS 5.6
!
*MSG,INFO
%/ DELETE LINES HAVING KEYPOINTS 0 0
/NOPR

! *** CREATE MACRO TO REDEFINE A LINE
!        (L,4.2 COMMAND DOESN'T WORK IN A *DO LOOP UNLESS IN A MACRO)
!        (2 NEARLY BLANK LINES ARE PART OF THE COMMAND !!!)
*CREATE,L42,MAC
L,4.2,_LINE,_K1,_K2
                  1
                  1
*END

!
! *** SAVE SELECTED SETS OF LINES AND KEYPOINTS
CM,LKZTEMP1,LINE
CM,LKZTEMP2,KP

! *** SELECT LINES NOT ATTACHED TO ANY KEYPOINTS
KSEL,ALL
LSLK
LSEL,INVE

! *** GET 1ST 2 DEFINED KEYPOINT NUMBERS
_K1 = 0
*GET,_K1,KP,_K1,NXTH
*GET,_K2,KP,_K1,NXTH

!      *** LOOP THROUGH LINES ***
/PREP7
_LINE = 0
*DO,_I,1,999999,1
   *GET,_LINE,LINE,_LINE,NXTH
   *IF,_LINE,EQ,0,EXIT
   L42
   LDEL,_LINE
*ENDDO

!
!      *** SELECT PREVIOUS SETS OF LINES AND KEYPOINTS
CMSEL,,LKZTEMP1
CMSEL,,LKZTEMP2
 
!      *** DELETE TEMPORARY COMPONENTS PARAMETERS MACRO
CMDEL,LKZTEMP1
CMDEL,LKZTEMP2
_LINE = $ _I = $ _K1 = $ _K2 =
/DEL,L42,MAC
/GOPR
