/com,COLLABORATIVE SOLUTIONS, INC. (CSI) DOES NOT GUARANTEE THE CORRECTNESS
/com,OR USEFULNESS OF THE RESULTS OBTAINED USING THIS INPUT FILE. CSI IS
/com,NOT LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/com,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/com,USEFULNESS OF THE RESULTS.
/com,
!com,AUTHOR:
!com,Sheldon Imaoka
!com,Collaborative Solutions, Inc.
!com,2303 W. 190th St.
!com,Redondo Beach, CA 90278
/com,
/com,DESCRIPTION OF INPUT FILE:
/com,  Simple example of traction applied on model
/com,

*get,STATGUI,active,,int
*if,STATGUI,eq,0,then
  /show,file
*endif
/title,Block w/ Traction
/view,1,1,1,1
/auto
/plopt,info,on
/plopt,leg2,off
/plopt,minm,off
/triad,lbot

*set,HEIGHT,10
*set,WIDTH ,10
*set,DEPTH ,2
/prep7
et,1,185
!------------------------------------
! Specify reduced integration
!------------------------------------
keyopt,1,2,1
r,1,
mp,ex  ,1,10e6
mp,nuxy,1,.3
mp,dens,1,.1/386.4

et,2,154
keyopt,2,4,1
keyopt,2,11,2
r,2,
mp,dens,2,0.0

block,,WIDTH,,HEIGHT,,DEPTH
vatt,1,1,1
!------------------------------------
! Specify ESIZE of 5 (really coarse)
! or of 1, and it runs OK.  Otherwise,
! default mesh needs mult > 1 for PCG
!------------------------------------
esize,1
mshape,0
mshkey,1
vmesh,all

asel,s,loc,y,0
da,all,ux

asel,s,loc,x,0
da,all,uy

asel,s,loc,z,0
da,all,uz

asel,s,loc,y,HEIGHT
asel,a,loc,x,WIDTH
nsla,s,1
type,2
real,2
mat ,2
esurf,all

nsel,s,loc,x,WIDTH
esln,s,1
sfe,all,5,pres,,100,,1

nsel,s,loc,y,HEIGHT
esln,s,1
sfe,all,5,pres,,100,1,

allsel,all
/psf,pres,norm,2,1
/pbc,u,1
sbctran
eplot
!/wait,5

finish

/solu
antype,static,new
eqslv,pcg,1e-8
solve
finish

/post1
set,last
/view,1,,,1
/title,von Mises Stress: sqrt(3) times Applied Pressure of 100 psi
plnsol,s,eqv,2
/title,Shear Stress: Same as Applied Pressure of 100 psi
plnsol,s,xy,2
finish

*if,STATGUI,eq,0,then
  /exit
*endif

