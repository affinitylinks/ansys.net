!
! ###############################################################
! #                                                           #
! # The macro was written in November-1999 by #
! # Dipl.-Ing. Peter Bastian #
! # #
! # Darmstadt University of Technology, Germany #
! # Department for Lightweight Construction #
! # #
! # #
! #                HOPE IT HELPS :-)                           #
! # #
! # #
! # #
! # The command-syntax is: #
! # #
! # S,STRESS-COMPOMENT,DIGITS,DECI,NCONT #
! # #
! # STRESS-COMPONENT is 1=x, 2=y, 3=z, 4=xy, 5=xz, 6=yz #
! # DIGITS is the number of digits after the decimal-point.#
! # DECI is the number of places for which a "0" should #
! #      be displayed. #
! # DECI is only valid, if DIGITS is blank or equals "0".#
! # NCONT is the number of contours to be generated #
! # (default=9) #
! #                                                           #
! # #
! # This macro displays results as continuos contours #
! # using the PLNSOL command. #
! # For the display of the numbers in the legend, you #
! # can define the number of contours and the number of #
! # digits after the decimal point, to be displayed. #
! # If you do not want any digits after the decimal point, #
! # you can define, how many places should be displayed #
! # with a "0". For example 2100 instead of 2123. #
! #                                            #
! # s,xy,2 gives the SX-Plot with the following format: #
! # SX=1238.34 #
! # s,xy,,2 gives the SX-Plot with the following format: #
! # SX=1200 #
! #                                                #
! # For access from the ansys-input-window copy the macro #
! # to the default macro-path or use the psearch-command #
! # to define the used path. #
! ###############################################################

/plots,info,auto !Display the legend if necessary
error=0 !Set the error-variable to zero

arg1typ=arg1
*get,partyp,'parm',arg1typ,type !Check the typ of the input

*if,partyp,eq,3,then !Set the stress-component when character
  *if,arg1,eq,'x',then
  stress='x'
  *elseif,arg1,eq,'y'
  stress='y'
  *elseif,arg1,eq,'z'
  stress='z'
  *elseif,arg1,eq,'xy'
  stress='xy'
  *elseif,arg1,eq,'xz'
  stress='xz'
  *elseif,arg1,eq,'yz'
  stress='yz' 
  *else !Display error-message
  *msg,error
  The command-syntax is "S,STRESS-COMPOMENT,DIGITS,DECI,NCONT" %/ &
  %/ &
  STRESS-COMPONENT is 1 or 'x' =x, 2 or 'y' =y, 3 or 'z' =z %/ &
  4 or 'xy' =xy, 5 or 'xz' =xz, 6 or 'yz' =yz. %/ &
  DIGITS is the number of digits after the decimal-point. %/ &
  DECI is the number of places for which a "0" should be displayed. %/ &
  NCONT is the number of contours to be generated (default=9) %/ &
  %/ &
  DECI is only valid, if DIGITS is blank or equals "0". %/ &
  %/ &  

  error=1 !Set error-variable to one
 *endif
*elseif,partyp,eq,0,then !
 *if,arg1,eq,1,then !Set the stress-component
  stress='x'
  *elseif,arg1,eq,2
  stress='y'
  *elseif,arg1,eq,3
  stress='z'
  *elseif,arg1,eq,4
  stress='xy'
  *elseif,arg1,eq,5
  stress='xz'
  *elseif,arg1,eq,6
  stress='yz'
  *else !Display error-message
  *msg,error
  The command-syntax is "S,STRESS-COMPOMENT,DIGITS,DECI,NCONT" %/ &
  %/ &
  STRESS-COMPONENT is 1 or 'x' =x, 2 or 'y' =y, 3 or 'z' =z %/ &
  4 or 'xy' =xy, 5 or 'xz' =xz, 6 or 'yz' =yz. %/ &
  DIGITS is the number of digits after the decimal-point. %/ &
  DECI is the number of places for which a "0" should be displayed. %/ &
  NCONT is the number of contours to be generated (default=9) %/ &
  %/ &
  DECI is only valid, if DIGITS is blank or equals "0". %/ &
  %/ &  

  error=1 !Set error-variable to one
 *endif
*else
 *msg,error
  The command-syntax is "S,STRESS-COMPOMENT,DIGITS,DECI,NCONT" %/ &
  %/ &
  STRESS-COMPONENT is 1 or 'x' =x, 2 or 'y' =y, 3 or 'z' =z %/ &
  4 or 'xy' =xy, 5 or 'xz' =xz, 6 or 'yz' =yz. %/ &
  DIGITS is the number of digits after the decimal-point. %/ &
  DECI is the number of places for which a "0" should be displayed. %/ &
  NCONT is the number of contours to be generated (default=9) %/ &
  %/ &
  DECI is only valid, if DIGITS is blank or equals "0". %/ &
  %/ &  

 error=1 !Set error-variable to one
*endif

*if,error,eq,0,then !Goto end of file and quit if error<>0
 digit=arg2 !How many digits after the decimal point
 deci=arg3 !How many places with "0" before the decimal point

 *if,arg4,le,0,then !Number of conturs to be generated (default=9)
 ncont=9
 *else
 ncont=arg4
 *endif

 nsort,s,%stress%,1 !Sort, refering to the stress-component
 *get,s%stress%min,sort,,min !Get the minimun stress-value
 *get,s%stress%max,sort,,max !Get the maximum stress-value
 range=(s%stress%max-s%stress%min) !Calculate the stress-range

 *if,digit,gt,0,then !Calculate increment and minimum stress to display
   vinc=nint((range/ncont)*(10**%digit%))/(10**%digit%) !for a display with a given number
   smin=nint(s%stress%min*(10**%digit%))/(10**%digit%) !of digits after the decimal point.
    
 *else !Calculate increment and minimum stress to display
   vinc=nint((range/ncont)/(10**%deci%))*(10**%deci%) !for a display with a given number of places
   correct=nint((range-ncont*vinc)/2) !to display with "0"
   smin=nint((s%stress%min+correct)/(10**%deci%))*(10**%deci%)

 *endif

 /cont,,ncont,smin,vinc !Define the user specified contours
 rsys,solu !Set the result-coordinate-system as calculated
 plnsol,s,%stress% !Generate the stressplot
*endif

/cont,,9,auto !Set the contour-display to auto for the next plot

