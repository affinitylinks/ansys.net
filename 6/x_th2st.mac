! =========================================
! Shen-Yeh Chen
! 7/01/1999
! =========================================
! This macro transfers the temperature distribution from a thermal
! analysis into structural temperature load. Only pre-selected nodes are dumped
! The output file name is x_th2st.tld
! (TH2ST == THermal 2 STructure)
!
! Syntax :
!
!  x_th2st
! 
! Output
!  file : x_th2st.tld
!
! External programs called :
!  None
! =========================================
/NOP
*GET,X_TH2ST_NN,NODE,,NUM,MAX
*GET,X_TH2ST_NC,NODE,,COUNT
*DIM,X_TH2ST_S,,X_TH2ST_NN
*DIM,X_TH2ST_T,,X_TH2ST_NN
*DIM,X_TH2ST_NID,,X_TH2ST_NN
*VGET,X_TH2ST_S(1),NODE,1,NSEL
*VFILL,X_TH2ST_NID(1),RAMP,1,1


*VMASK,X_TH2ST_S(1)
*VGET,X_TH2ST_T(1),NODE,1,TEMP

*CFOPEN,x_th2st,tld

*VWRITE,'!',X_TH2ST_NN,X_TH2ST_NC
(A1,I10,I10)

*VMASK,X_TH2ST_S(1)
*VWRITE,' BF,',X_TH2ST_NID(1),', TEMP,',X_TH2ST_T(1)
(A4,I8,A7,E16.7)

*CFCLOSE
X_TH2ST_T(1)=
X_TH2ST_S(1)=
X_TH2ST_NID(1)=
X_TH2ST_NN=
X_TH2ST_NC=



/eof
===================================
ANSYS Macro Convention
===================================
In order to avoid conflicting betweens the macro, the following convention
has been adapted for the ANSYS macros here.

1. All macro will have name begin with string "x_". This is to avoid name collision
   with the existing ANSYS commands.
2. There is no naming convention for global variables. All variables are
   considered local
3. The names of the internal variables will be one of the following
   (1) Begin with the name of the macro itself first. That is, for the 
       macro x_noffst.mac, the internal variables are X_NOFFST_X, 
       X_NOFFST_Y, X_NOFFST_Z etc.
   (2) Begin with "Z"
   (3) ARG1 to AR99
   Note that, ARG1 to AR99 can be heavily used according to the
   programming convenience (for example, as a loop counter). They are not
   tracked. There is no rule for using them either.
4. All local variables, unless necessary for further processing, WILL be
   deleted in the end of the macro. However, when such variables may
   be necessary for later process, they will be kept. These variables
   are listed in the header of the macro.
5. When there is output file from the macro, their names will begin with 
   the name of the macro itself first.
6. The macro will not call each other. That is, each macro will be self-contained.


Shen-Yeh Chen
www.FEA-Optimization.com
