/com,COLLABORATIVE SOLUTIONS, INC. (CSI) DOES NOT GUARANTEE THE CORRECTNESS
/com,OR USEFULNESS OF THE RESULTS OBTAINED USING THE EXAMPLE. CSI IS NOT
/com,LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/com,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/com,USEFULNESS OF THE RESULTS.
/com,
!com,AUTHOR:
!com,David Haberman
!com,Collaborative Solutions, Inc.
!com,5962 La Place Court Suite 100
!com,Carlsbad CA 92009
!com,760-431-4815
!com,david.haberman@csi-cae.com
/com,
/com,DESCRIPTION 
/com,Example of running a random vibration analsyis in ANSYS.
/com,Acceleration were applied with the large mass method and
/com,f=ma, where m is the large mass and a is the desired acceleration
/com,

finish
/clear
!!!!Example of random vibrations free-free modal large
!!!!mass method        
! 
!!!!Build Model!!!!
/PREP7  
RECTNG,10,,1,,  
ET,1,SHELL181   
KEYOPT,1,3,2  ! full intergration
R,1,.1, , , , , ,  
RMORE, , , , , , ,  
/MPLIB,READ,d:\apps\ansys56\matlib  
/MPLIB,WRITE,    
MAT,1,  
! /INPUT,d:\apps\ansys56\matlib\Stl_AISI-304.BIN_MPL
! /COM,Typical material properties for DEMO purposes only 
! /COM,ANSYS REVISION  5.3    UP042296 TEST    16:35:50    04/23/1996 
/NOP
! /COM,Internal UNITS set at file creation time = BIN 
TBDEL,ALL,_MATL 
MPDEL,ALL,_MATL 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,EX  ,_MATL , 1,  27992720.0    , 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,NUXY,_MATL , 1, 0.290000000    , 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,ALPX,_MATL , 1, 9.888888889E-06, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,DENS,_MATL , 1, 7.514795200E-04, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,KXX ,_MATL , 1, 2.182244000E-04, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,C   ,_MATL , 1,  46.2864080    , 
!
esize,.5   
amesh,all   
numoff,node,100
!!!!Add large mass!!!
ET,2,MASS21 
KEYOPT,2,2,0
KEYOPT,2,3,0
R,2,1e5,1e5,1e5,1e5,1e5,1e5, 
n,1,-1   
type,2  
real,2  
mat,2   
e,1
nsel,s,loc,x,-1,0   
cerig,1,all,all
numcmp,all   
!!!!!!!!!!!!!!!!!!!!!!!!
FINISH  
!!!!Modal Analysis!!!!
/SOLU   
ANTYPE,2
MXPAND,10, , ,1 
MODOPT,lanb,10,1,0, ,OFF, ,0
allsel,all
SOLVE   
finish
!!!!!!!PSD Analysis!!!!!!!!!!!!!!!!!!!!!!!!!!!
/sol
anty,spec
spopt,psd,,on
psdu,1,forc                    ! force based psd
psdf,1,,2,10,50,200,400
! Units (lbs^2)^2/Hz
psdv,1,(20*1e10),(80*1e10),(80*1e10),(50*1e10),(20*1e10)
dmpr,.03
f,1,fz,1
pfac,1,node
psdcom,.001,5
psdr,disp,rel ! calculate disp results
psdr,velo,rel ! calculate velocity results
psdr,acel,rel ! calculate acceleration results
save
solv
fini
!!!!!!!!Post Process Post26 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
/POST26 
numvar,50
STORE,PSD,10,  
!*  
NSOL,2,24,U,Z,   
!*  
RPSD,3,2,,1,2,  ! displacement
RPSD,4,2,,2,2,  ! velocity
RPSD,5,2,,3,2,  ! acceleration
!
! Intergrate rspd over freq.
INT1,6,3,1, , , , ,1,1, ,   
INT1,7,4,1, , , , ,1,1, ,   
INT1,8,5,1, , , , ,1,1, ,   
!
! Get the area under the rpsd curve (intergration)
!
*get,uz6a,vari,6,extrem,vlast
*get,vz6a,vari,7,extrem,vlast
*get,az6a,vari,8,extrem,vlast
!
! Calculate the 1 sigma results by taking the 
! square root of the area under the rpsd curve
!
uz6b=sqrt(uz6a)  ! 1 sigma displacements
vz6b=sqrt(vz6a)  ! 1 sigma velocities
az6b=sqrt(az6a)  ! 1 sigma accelerations
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!
!!!!!!!Post Process Post1 !!!!!!!!!!!!!!!!!!!
finish
/post1
set,3,1,
*get,uz6,node,24,u,z ! 1 sigma displacement
set,4,1
*get,vz6,node,24,u,z ! 1 sigma velocities
set,5,1
*get,az6,node,24,u,z ! 1 sigma accelerations
finish
!
! compare the following 1 sigma results
!
! post1     post26
! uz6   =   uz6b
! vz6   =   vz6b
! az6   =   az6b
!

/eof


 MODE    FREQUENCY (HERTZ)


  FREQUENCY RANGE REQUESTED=   10 MODES ABOVE   1.00000     HERTZ

   1     31.39598268636
   2     197.7769277121
   3     310.3048451269
   4     560.2065017785
   5     609.3867969606
   6     1118.292751571
   7     1842.107031081
   8     1880.162369247
   9     1895.676379420
  10     2923.145450138


NAME         VALUE         TYPE     DIMENSIONS
  AZ6       617.123490      SCALAR
  AZ6A      381172.587      SCALAR
  AZ6B      617.391762      SCALAR
  UZ6      1.029164924E-02  SCALAR
  UZ6A     1.060779896E-04  SCALAR
  UZ6B     1.029941696E-02  SCALAR
  VZ6       2.06301372      SCALAR
  VZ6A      4.25730857      SCALAR
  VZ6B      2.06332464      SCALAR



