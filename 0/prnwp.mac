!  Macro to align working plane with principal stress directions at a node
!  Usage: PRNWP,nodn
!	
!	   nodn= Node number which to use for alignment, leave blank for picking
!
!  Notes:  Should be used only in /POST1.  Stress results should be available for input node,
!           stress data is not available for midside nodes
!          Node must be selected.
!          WS X dir is in direction of 1st Principal Stress
!          WS Y dir is in direction of 2nd Principal Stress
!          WS Z dir is is direction of 3rd Principal Stress
!         
!
/pmacro
*get,_rout,active,,rout         ! Check for /POST1
*if,_rout,ne,31,then
*MSG,WARN
PRNWP only available in /POST1
*else
*if,arg1,eq,0,then
cm,_ntemp,node
nsel,r,p
arg1=ndnext(0)
cmsel,s,_ntemp
*endif
*get,ar31,active,,csys
*get,ar32,active,,rsys
csys,0
rsys,0
*del,stcmp_
*del,dirc_
*dim,stcmp_,array,1,6
*get,stcmp_(1,1),node,arg1,s,x
*get,stcmp_(1,2),node,arg1,s,y
*get,stcmp_(1,3),node,arg1,s,z
*get,stcmp_(1,4),node,arg1,s,xy
*get,stcmp_(1,5),node,arg1,s,yz
*get,stcmp_(1,6),node,arg1,s,xz
*dim,dirc_,array,1,9
*vfun,dirc_(1,1),dircos,stcmp_(1,1)
x_=nx(arg1)
y_=ny(arg1)
z_=nz(arg1)
wplane,-1,x_,y_,z_,x_+dirc_(1,1),y_+dirc_(1,2),z_+dirc_(1,3),x_+dirc_(1,4),y_+dirc_(1,5),z_+dirc_(1,6)
!cswpla,11,0  !Uncomment this line to create coord system 11 aligned with principal stresses
csys,ar31
rsys,ar32
*del,stcmp_
*del,dirc_
x_=
y_=
z_=
*endif