! VSLM.MAC Revision 3.
! By Bram Weisman Aug 6, 2003
! Developed on ANSYS Ed v 5.3
! Selects volumes by Mesh Status
! Syntax VSLM,n
! where:
!  n=0 Selects all meshed volumes from full set
!  n=1 Reselects meshed volumes from current set
!  n=2 Unselects meshed volumes from current set
!  n=3 Unselects all meshed volumes from full set
!
! Thanks to John Crawford for VSLM Rev 1. which provided me years of use.
! Rev 1 was very useful, but slow for large numbers of volumes since it 
! had to loop on each volume.
!
! Thanks also to Jim P. for showing me *vput,,volume,,vsel, worked when I had
! convinced myself it didn't.
! 
! Special thanks to John Swanson for creating ANSYS, which generally gives me great joy.
! 
! Note: To be cautious, I took care to ensure that vsel values of zero
! remained unchanged, even though it appears they cannot be changed.  Stated
! another way, this macro will only affect values of -1 or 1 in the vsel database.
! This was to done to ensure the changes are valid.
!
/nopr
*del,vmstat_,nopr 
*dim,vmstat_,,vlinqr(0,14),6
*vget,vmstat_(1,1),volume,,vsel  !Column 1, current select status 
*vabs,0,1,0 
*voper,vmstat_(1,2),vmstat_(1,1),add,vmstat_(1,2)  !Column 2, exist status 
*vmask,vmstat_(1,2)!Mask only exist volumes 
*vget,vmstat_(1,3),volume,,attr,nelm!Column 3, Number of Elems 
*vmask,vmstat_(1,3) 
*voper,vmstat_(1,4),vmstat_(1,2),div,vmstat_(1,2)  !If col 3 is non zero then col 4 = 1 
!
*if,arg1,eq,0,then !Select All Meshed volumes
*voper,vmstat_(1,6),vmstat_(1,6),sub,vmstat_(1,2),  !Column 6, neg exist status (all unsel)
*vmask,vmstat_(1,4)
*vfun,vmstat_(1,6),copy,vmstat_(1,4) 
vsel,all
*vput,vmstat_(1,6),volume,1,vsel
!
*elseif,arg1,eq,1,then !Reselect all meshed volumes from Current Set
*voper,vmstat_(1,6),vmstat_(1,6),sub,vmstat_(1,2),  !Column 6, neg exist status (all unsel)
*vmask,vmstat_(1,1)
*vfun,vmstat_(1,5),copy,vmstat_(1,4)
*vmask,vmstat_(1,5)
*vfun,vmstat_(1,6),copy,vmstat_(1,5)
vsel,all
*vput,vmstat_(1,6),volume,1,vsel
!
*elseif,arg1,eq,2,then !Unselect all meshed volumes from current set
*vabs,0,0,0 
*vget,vmstat_(1,6),volume,,vsel  !Column 6, current select status 
*vmask,vmstat_(1,1)
*voper,vmstat_(1,6),vmstat_(1,6),sub,vmstat_(1,4) 
*vmask,vmstat_(1,1)
*voper,vmstat_(1,6),vmstat_(1,6),sub,vmstat_(1,4) 
vsel,all
*vput,vmstat_(1,6),volume,1,vsel
!
*elseif,arg1,eq,3,then !Select all Un-meshed volumes
*voper,vmstat_(1,6),vmstat_(1,6),sub,vmstat_(1,2),  !Column 6, neg exist status (all unsel)
*vmask,vmstat_(1,4)
*vfun,vmstat_(1,6),copy,vmstat_(1,4) 
vsel,all
*vput,vmstat_(1,6),volume,1,vsel
vsel,invert
!
*else
:syntax
*msg,warn
Invalid input. Please use the syntax: VSLM,n%/&
where%/&
%5X n=0 Selects all meshed volumes from full set%/&
%5X n=1 Reselects meshed volumes from current set%/&
%5X n=2 Unselects meshed volumes from current set%/&
%5X n=3 Unselects all meshed volumes from full set%/&
%12X (selects all unmeshed from full)
*endif 
*del,vmstat_,nopr
/go
/eof
