/COM,THE AUTHOR DOES NOT GUARANTEE THE CORRECTNESS
/COM,OR USEFULNESS OF THE RESULTS OBTAINED USING THE EXAMPLE. THE AUTHOR IS NOT
/COM,LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/COM,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/COM,USEFULNESS OF THE RESULTS.
/COM,
!COM,AUTHOR:
!COM,MOHAMMAD A GHARAIBEH
!COM,MECHANICAL ENGINEERING DEPARTMENT
!COM,THOMAS J. WATSON SCHOOL OF ENGINEERING & APPLIED SCIENCE
!COM,STATE UNIVERSITY OF NEW YORK AT BINGHAMTON
!COM,BINGHAMTON, NY 13902-6000
!COM,EMAIL: MGHARAI1@BINGHAMTON.EDU
/COM,
/COM,DESCRIPTION 
/COM,HARMONIC LINEAR SWEEP EXAMPLE. MODE SUPER-POSITION WITH THE ENFORCED MOTION METHOD (EMM)
/COM,EMM IS AVAILABLE IN ANSYS R14.5 AND NEWER
/COM,ISOTROPIC SQUARED ELASTIC PLATE WITH 4-EDGE CLAMPED BOUNDARY CONDITIONS (CCCC)


FINISH
/CLEAR
/OUT,EMM_SUMMARY,OUT
/TITLE, ENFORCED MOTION METHOD 
/UNIT,SI
/FILNAME,HARMIC_EMM,1
/PREP7                                                        
! ELEMENT TYPE    
! **************
ET,1,SOLID185	
KEYOPT,1,2,3

! MATERIALS PROPERTIES
! *********************
MP,EX,1,210E9	   
MP,PRXY,1,0.3
MP,DENS,1,7850


! GEOMETRY 
! *********
L=10E-2	!PLATE LENGTH 
W=10E-2 !PLATE WIDTH 
T=0.1E-2 !PLATE THICKNESS 

! Acceleration input
NG = 1 ! Number of g's
G = 9.81 ! Gravity acceleration
TOTG = NG*G

! PLATE MODELING
! ****************
BLOCK,0,L,0,W,0,T

! MESH CONTROL
! *************
LSEL,S,LOC,Z,0
LSEL,A,LOC,Z,T
LESIZE,ALL,,,60
ALLSEL,ALL
LSEL,S,LENGTH,,T
LESIZE,ALL,,,4
ALLSEL,ALL

! MESHING
! *******
VSEL,ALL
VATT,1,,1
VMESH,ALL
ALLSEL,ALL

! BC'S - CCCC
! *****
NSEL,S,LOC,X,0
NSEL,A,LOC,X,L
NSEL,A,LOC,Y,0
NSEL,A,LOC,Y,W
D,ALL,ALL,0
CM,BASE_NODES,NODE ! BASE NODES
ALLSEL,ALL



! MODE SUPERPOSITION HARMONIC ANALYSIS SOLUTION
! **********************************************
! MODAL ANALYSIS
! ****************
/SOLU
ANTYPE,2
NMODE=10
MODOPT,LANB,NMODE  
EQSLV,SPAR  
MXPAND,NMODE , , ,1 
LUMPM,0 
PSTRES,0
MODCONT,,ON ! ACTIVATE ENFORCED MOTION
CMSEL,S,BASE_NODES,NODE
D,ALL,UZ,1      ! DEFINE ENFORCED MOTION 1 TO BE APPLIED ON BASE NODES IN UZ
ALLSEL,ALL
SOLVE 
FINISH

! HARMONIC ANALYSIS
! ******************
FREQ1=1			! FIRST FREQUENCY IN RANGE
FREQ2=10000			! LAST FREQUENCY IN RANGE
FREQSTEP=(FREQ2-FREQ1)*1	! SUBSTEPS
/SOL 

ANTYPE,3 
HROPT,MSUP, , ,0
HROUT,ON
DVAL,1,ACC,TOTG  ! APPLY ACCELERATION IN Z DIRECTION
HARFRQ,FREQ1,FREQ2,
NSUBST,FREQSTEP,  
KBC,1     
ALPHAD,0,   
BETAD,0,
DMPRAT,0.005
SOLVE 
SAVE
FINISH 

! TIME HISTORY POST-PROCESSING
! ****************************
/POST26 
file,,rfrq
NSOL,2,5702,U,Z,                     !  NODE 5702 DISPLACEMENT
/AXLAB,X,Frequency [Hz]  
/AXLAB,Y,Absolute Response [m]  
/GROPT,LOGX,OFF  
/GROPT,LOGY,ON 
PLVAR,2


