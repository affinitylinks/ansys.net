! ASLM.MAC Revision 3.
! By Bram Weisman Aug 6, 2003
! Developed on ANSYS Ed v 5.3
! Selects Areas by Mesh Status
! Syntax ASLM,n
! where:
!  n=0 Selects all meshed areas from full set
!  n=1 Reselects meshed areas from current set
!  n=2 Unselects meshed areas from current set
!  n=3 Unselects all meshed areas from full set (selects all unmeshed from full)
!
! Thanks to John Crawford for ASLM Rev 1. which provided me years of use.
! Rev 1 was very useful, but slow for large numbers of areas since it 
! had to loop on each area.
!
! Thanks also to Jim P. for showing me *vput,,area,,asel, worked when I had
! convinced myself it didn't.
!
! Special thanks to John Swanson for creating ANSYS, which generally gives me great joy.
! 
! Note: To be cautious, I took care to ensure that asel values of zero
! remained unchanged, even though it appears they cannot be changed.  Stated
! another way, this macro will only affect values of -1 or 1 in the asel database.
! This was to done to ensure the changes are valid.
!
/nopr
*del,amstat_,nopr 
*dim,amstat_,,arinqr(0,14),6
*vget,amstat_(1,1),area,,asel  !Column 1, current select status 
*vabs,0,1,0 
*voper,amstat_(1,2),amstat_(1,1),add,amstat_(1,2)  !Column 2, exist status 
*vmask,amstat_(1,2)!Mask only exist areas 
*vget,amstat_(1,3),area,,attr,nelm!Column 3, Number of Elems 
*vmask,amstat_(1,3) 
*voper,amstat_(1,4),amstat_(1,2),div,amstat_(1,2)  !If col 3 is non zero then col 4 = 1 
!
*if,arg1,eq,0,then !Select All Meshed Areas
*voper,amstat_(1,6),amstat_(1,6),sub,amstat_(1,2),  !Column 6, neg exist status (all unsel)
*vmask,amstat_(1,4)
*vfun,amstat_(1,6),copy,amstat_(1,4)
asel,all
*vput,amstat_(1,6),area,1,asel
!
*elseif,arg1,eq,1,then !Reselect all meshed Areas from Current Set
*voper,amstat_(1,6),amstat_(1,6),sub,amstat_(1,2),  !Column 6, neg exist status (all unsel)
*vmask,amstat_(1,1)
*vfun,amstat_(1,5),copy,amstat_(1,4)
*vmask,amstat_(1,5)
*vfun,amstat_(1,6),copy,amstat_(1,5)
asel,all
*vput,amstat_(1,6),area,1,asel
!
*elseif,arg1,eq,2,then !Unselect all meshed areas from current set
*vabs,0,0,0 
*vget,amstat_(1,6),area,,asel  !Column 6, current select status 
*vmask,amstat_(1,1)
*voper,amstat_(1,6),amstat_(1,6),sub,amstat_(1,4) 
*vmask,amstat_(1,1)
*voper,amstat_(1,6),amstat_(1,6),sub,amstat_(1,4) 
asel,all
*vput,amstat_(1,6),area,1,asel
!
*elseif,arg1,eq,3,then !Select all Un-meshed areas
*voper,amstat_(1,6),amstat_(1,6),sub,amstat_(1,2),  !Column 6, neg exist status (all unsel)
*vmask,amstat_(1,4)
*vfun,amstat_(1,6),copy,amstat_(1,4) 
asel,all
*vput,amstat_(1,6),area,1,asel
asel,invert
!
*else
:syntax
*msg,warn
Invalid input. Please use the syntax: ASLM,n%/&
where%/&
%5X n=0 Selects all meshed areas from full set%/&
%5X n=1 Reselects meshed areas from current set%/&
%5X n=2 Unselects meshed areas from current set%/&
%5X n=3 Unselects all meshed areas from full set%/&
%12X (selects all unmeshed from full)
*endif 
*del,amstat_,nopr
/go
/eof
