!=================================================================
!===macro to connect the nodes of fluid pipe elements (FLUID116)
!===to thermal surface effect elements (SURF151, SURF152)
!
!===To run this macro, have the *NODES* of the desired fluid pipe
!===elements selected, and the surface effect *ELEMENTS* for which
!===linking to the fluid pipe elements is desired selected.
!
!===created by: Jeroen Valensa
!=== Modine Mfg. Co.
!===last revised: 6/06/2007
!=================================================================

cm,esurf_,elem
cm,nfluid_,node
/output,deleteme,txt

tpcount=0
boolean=1
*dowhile,boolean
tpcount=tpcount+1
*get,_etype,elem,elnext(0),attr,type
cm,temp1,elem
esel,r,type,,_etype
cm,esurf_%tpcount%,elem
cmsel,s,temp1
cmsel,u,esurf_%tpcount%
*get,boolean,elem,,count
*enddo

*dim,etarray,array,elmiqr(0,14),tpcount
*dim,xnodearray,array,tpcount,1
*dim,enamarray,array,tpcount,6
*dim,xnode,array,elmiqr(0,14),1

*do,loopcounter,1,tpcount
cmsel,s,esurf_%loopcounter%
*vget,etarray(1,loopcounter),elem,1,esel
*vfun,etarray(1,loopcounter),not,etarray(1,loopcounter)
*vfun,etarray(1,loopcounter),not,etarray(1,loopcounter)
*get,_etype,elem,elnext(0),attr,type
*get,elemname_,etyp,_etype,attr,enam
enamarray(loopcounter,1)=elemname_
enamarray(loopcounter,2)=elemname_
*if,elemname_,eq,151,then
keyopt,_etype,5,0
keyopt,_etype,6,0
*endif
*if,elemname_,eq,152,then
keyopt,_etype,5,0
keyopt,_etype,6,0
*endif
*get,xnodeset,etyp,_etype,attr,kop4 !midside node status
enamarray(loopcounter,4)=9-4*xnodeset
enamarray(loopcounter,5)=4-xnodeset
enamarray(loopcounter,6)=_etype
*enddo

*voper,enamarray(1,2),152,sub,enamarray(1,1)
*voper,enamarray(1,1),enamarray(1,1),sub,151
*voper,enamarray(1,3),enamarray(1,1),mult,enamarray(1,2)
*vfun,enamarray(1,3),not,enamarray(1,3)
*voper,xnodearray(1,1),enamarray(1,1),mult,enamarray(1,4)
*vcum,1
*voper,xnodearray(1,1),enamarray(1,2),mult,enamarray(1,5)
*voper,xnodearray(1,1),xnodearray(1,1),mult,enamarray(1,3)
*moper,xnode,etarray,mult,xnodearray

cmsel,s,esurf_
*dim,fullarray,array,elmiqr(0,14),5
*vfill,fullarray(1,1),ramp,1,1
*vget,fullarray(1,2),elem,1,cent,x
*vget,fullarray(1,3),elem,1,cent,y
*vget,fullarray(1,4),elem,1,cent,z
*vfun,fullarray(1,5),not,xnode(1,1)
*vfun,fullarray(1,5),not,fullarray(1,5)
*vscfun,enum,sum,fullarray(1,5)
*vfun,fullarray(1,5),copy,xnode(1,1)
*dim,reduced,array,enum,8
*dim,exnode,array,enum,3
*vmask,fullarray(1,5)
*vfun,reduced(1,1),comp,fullarray(1,1)
*vmask,fullarray(1,5)
*vfun,reduced(1,2),comp,fullarray(1,2)
*vmask,fullarray(1,5)
*vfun,reduced(1,3),comp,fullarray(1,3)
*vmask,fullarray(1,5)
*vfun,reduced(1,4),comp,fullarray(1,4)
*vmask,fullarray(1,5)
*vfun,exnode(1,3),comp,fullarray(1,5)
*voper,exnode(1,3),exnode(1,3),mult,-1
*del,fullarray,,nopr
*vfill,reduced(1,5),ramp,1e6,0

*get,nnum,node,,count
*dim,allnodes,array,ndinqr(0,14),5
*dim,nodelocs,array,nnum,4
*vfun,exnode(1,1),copy,reduced(1,1)
*vfill,allnodes(1,1),ramp,1,1
*vget,allnodes(1,2),node,1,loc,x
*vget,allnodes(1,3),node,1,loc,y
*vget,allnodes(1,4),node,1,loc,z
*vget,allnodes(1,5),node,1,nsel
*vmask,allnodes(1,5)
*vfun,nodelocs(1,1),comp,allnodes(1,1)
*vmask,allnodes(1,5)
*vfun,nodelocs(1,2),comp,allnodes(1,2)
*vmask,allnodes(1,5)
*vfun,nodelocs(1,3),comp,allnodes(1,3)
*vmask,allnodes(1,5)
*vfun,nodelocs(1,4),comp,allnodes(1,4)
*del,allnodes,,nopr

*do,counter,1,nnum
*vfill,reduced(1,6),ramp,nodelocs(counter,2),0
*voper,reduced(1,6),reduced(1,6),sub,reduced(1,2)
*vfun,reduced(1,6),pwr,reduced(1,6),2
*vfill,reduced(1,7),ramp,nodelocs(counter,3),0
*voper,reduced(1,7),reduced(1,7),sub,reduced(1,3)
*vcum,1
*vfun,reduced(1,6),pwr,reduced(1,7),2
*vfill,reduced(1,7),ramp,nodelocs(counter,4),0
*voper,reduced(1,7),reduced(1,7),sub,reduced(1,4)
*vcum,1
*vfun,reduced(1,6),pwr,reduced(1,7),2
*vfun,reduced(1,6),pwr,reduced(1,6),1/2
*voper,reduced(1,7),reduced(1,6),sub,reduced(1,5)
*vfun,reduced(1,7),not,reduced(1,7)
*vfun,reduced(1,8),not,reduced(1,7)
*voper,exnode(1,2),exnode(1,2),mult,reduced(1,8)
*vfact,1,1,nodelocs(counter,1)
*voper,exnode(1,2),exnode(1,2),add,reduced(1,7)
*voper,reduced(1,5),reduced(1,5),mult,reduced(1,8)
*vcum,1
*voper,reduced(1,5),reduced(1,6),mult,reduced(1,7)
*enddo

*do,counter,1,tpcount
*get,elemname_,etyp,enamarray(counter,6),attr,enam
*if,elemname_,ge,151,then
*if,elemname_,le,152,then
keyopt,enamarray(counter,6),5,1
keyopt,enamarray(counter,6),6,0
*endif
*endif
*enddo

cmsel,s,esurf_
nsle
cmsel,a,nfluid_

emodif,exnode(1:enum,1),exnode(1:enum,3),exnode(1:enum,2)

*del,exnode,,nopr
*del,reduced,,nopr
*del,nodelocs,,nopr

cmsel,s,esurf_
cmsel,s,nfluid_
cmdele,esurf_
cmdele,nfluid_
*do,counter,1,tpcount
cmdele,esurf_%counter%
*enddo
*del,_etype,,nopr
*del,counter,,nopr
*del,maxelem,,nopr
*del,xnodeset,,nopr
*del,tpcount,,nopr
*del,boolean,,nopr
*del,loopcounter,,nopr
*del,elemname,,nopr
*del,nnum,,nopr
*del,maxnode,,nopr
*del,indexloc,,nopr
*dim,etarray
*dim,xnodearray
*dim,enamarray
*dim,xnode

/output
