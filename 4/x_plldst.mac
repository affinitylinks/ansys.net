! =========================================
! Shen-Yeh Chen
! 05/01/1998
! =========================================
! This macro will get/plot (reponse vs load level) graph on the creen
! Using this macor is similar to use the *GET command like
!   *GET,_Parname,_Entity,_EntityID,_Item,_Comp
! For example, if you issue the following command
!   x_plsbst,      , _BgnLdSet,_EndLdSet,'NODE',100,'U','X'
! The macro will do the following internally
!   *GET,X_PLLDST(_LdSet,1),NODE,100,U,X
! The index _LdSet will go through the loop from loadset number _BgnLdSet to _EndLdSet
! The time step ratio will be stored in the "0" column of the matrix X_PLLDST
! The value get for each time step will be stored in the "1" column of the matrix X_PLLDST
! And the table data will be written into the file x_plldst.csv  
! --------------------------------------------------------------------------   
!
! Syntax :
!   x_plsbst,      , _BgnLdSet,_EndLdSet,'_Entity',_EntityID,'_Item','_Comp'
!
! Input :
!   _BgnLdSet : Beginning load set ID
!   _End      : End load set ID
!   '_Entity',_EntityID,'_Item','_Comp'
!             : see ANSYS manual for *GET,_Parname,_Entity,_EntityID,_Item,_Comp
! 
! Output :
!   file : x_plldst.csv
!
! Example
!     x_plsbst,      , _BgnLdSet,_EndLdSet,'NODE',_NodeID,'U','X'
!     x_plsbst,      ,1,5,'NODE',100,'S','EQV'
!             : plot/get time vs equivalent stress for node ID 100
!
! External programs called :
!  None
! =========================================
AR31=ARG3-ARG2+1
*GET,AR61,PARM,X_PLSBST,TYPE
*IF,AR61,NE,2,THEN
 *DIM,X_PLLDST,table,AR31+1,1
*ENDIF
!
!
AR21=ARG4
AR22=ARG5
AR23=ARG6
AR24=ARG7
!
/POST1
AR32=1
AR41=0.0
X_PLLDST(1,0)=0.0
X_PLLDST(1,1)=0.0
*DO,AR33,ARG2,ARG3,1
 SET,AR33
 AR32=AR32+1
 *GET,AR42,ACTIVE,0,SOLU,DTIME
 AR41=AR41+AR42
 X_PLLDST(AR32,0)=AR41
 *GET,X_PLLDST(AR32,1),%AR21%,AR22,%AR23%,%AR24%
*ENDDO
/XRANGE,0,AR41
*VPLOT,X_PLLDST(1,0),X_PLLDST(1,1)
!
*CFOPEN,x_plsbst,csv
*VWRITE,X_PLLDST(1,0),X_PLLDST(1,1)
(1P,1X,E15.6,',',1X,E15.6)
*CFOPEN




/eof
===================================
ANSYS Macro Convention
===================================
In order to avoid conflicting betweens the macro, the following convention
has been adapted for the ANSYS macros here.

1. All macro will have name begin with string "x_". This is to avoid name collision
   with the existing ANSYS commands.
2. There is no naming convention for global variables. All variables are
   considered local
3. The names of the internal variables will be one of the following
   (1) Begin with the name of the macro itself first. That is, for the 
       macro x_noffst.mac, the internal variables are X_NOFFST_X, 
       X_NOFFST_Y, X_NOFFST_Z etc.
   (2) Begin with "Z"
   (3) ARG1 to AR99
   Note that, ARG1 to AR99 can be heavily used according to the
   programming convenience (for example, as a loop counter). They are not
   tracked. There is no rule for using them either.
4. All local variables, unless necessary for further processing, WILL be
   deleted in the end of the macro. However, when such variables may
   be necessary for later process, they will be kept. These variables
   are listed in the header of the macro.
5. When there is output file from the macro, their names will begin with 
   the name of the macro itself first.
6. The macro will not call each other. That is, each macro will be self-contained.


Shen-Yeh Chen
www.FEA-Optimization.com
