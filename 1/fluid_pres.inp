/com,COLLABORATIVE SOLUTIONS, INC. (CSI) DOES NOT GUARANTEE THE CORRECTNESS
/com,OR USEFULNESS OF THE RESULTS OBTAINED USING THIS INPUT FILE. CSI IS
/com,NOT LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/com,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/com,USEFULNESS OF THE RESULTS.
/com,
!com,AUTHOR:
!com,Sheldon Imaoka
!com,Collaborative Solutions, Inc.
!com,2303 W. 190th St.
!com,Redondo Beach, CA 90278
/com,
/com,DESCRIPTION OF INPUT FILE:
/com,  Shows "fluid pressure" feature of surf-to-surf contact
/com,  This is a beta feature, undocumented at 5.6
/com,

finish
/clear

WIDTH    = 10
OFFST    = 10

E_SOLID  = 45
E_CONTA  = 173

/graph,power
/triad,lbot
/auto
/view,1,1,2,3
/pnum,type,1
/num,1

/title,Fluid pressure applied to surf-to-surf contact elements

/prep7
et,1,E_SOLID
r,1,.1
mp,ex  ,1,10e6
mp,nuxy,1,.3

et,2,170
et,3,E_CONTA
keyopt,2,1,0
keyopt,3,12,0
r,3
mp,mu  ,3,1e-31

et,4,170
et,5,E_CONTA
keyopt,4,1,0
keyopt,5,12,0
r,5
mp,mu  ,5,1e-31

block,,WIDTH,,WIDTH,,WIDTH
block,,WIDTH,WIDTH+OFFST,WIDTH*2+OFFST,,WIDTH

esize,2
vatt,1,1,1
vmesh,all

vsel,s,loc,y,0,WIDTH
aslv
asel,r,loc,y,WIDTH
nsla,s,1
type,2
real,3
mat ,3
esurf
esel,u,type,,2
type,5
real,5
mat ,5
esurf

vsel,s,loc,y,WIDTH+OFFST,WIDTH*2+OFFST
aslv
asel,r,loc,y,WIDTH+OFFST
nsla,s,1
type,3
real,3
mat ,3
esurf
esel,u,type,,3
type,4
real,5
mat ,5
esurf

allsel,all
finish

/solu
antype,static
nlgeom,on
rescon,define,none
nsubst,1,1000,1
outres,all,last

nsel,s,loc,y,0
d,all,uy

nsel,s,loc,y,2*WIDTH+OFFST
d,all,uy

nsel,s,loc,x,0
d,all,ux

nsel,s,loc,z,0
d,all,uz

esel,s,ename,,E_CONTA
sfe,all,1,pres,,100

allsel,all
solve
finish

/post1
set,last
esel,s,ename,,E_SOLID

!----------------------------------------
! should be PRES=100 for OFFST specified
! PRES=0 otherwise
! also plot u,y to see effect
!----------------------------------------
plnsol,s,y


