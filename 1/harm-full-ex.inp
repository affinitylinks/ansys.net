/com,COLLABORATIVE SOLUTIONS, INC. (CSI) DOES NOT GUARANTEE THE CORRECTNESS
/com,OR USEFULNESS OF THE RESULTS OBTAINED USING THE EXAMPLE. CSI IS NOT
/com,LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/com,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/com,USEFULNESS OF THE RESULTS.
/com,
!com,AUTHOR:
!com,David Haberman
!com,Collaborative Solutions, Inc.
!com,5962 La Place Court Suite 100
!com,Carlsbad CA 92009
!com,760-431-4815
!com,david.haberman@csi-cae.com
/com,
/com,DESCRIPTION 
/com,
/com,Direct displacement method (full harmonic with applied displacment).
/com,Modal analysis then frequency sweep.
/com,Result are scaled to get acceleration, displacement, and g's vs
/com,frequency.
/com,

finish
/clear
/PREP7  

g=386.4            ! gravity in/sec^2
totg=1             ! load in g's
xa=g*totg          ! acceleration in/sec^2
pi=3.141592654     ! pi

/title,DIRECT DISPLACEMENT METHOD (UNIT LOAD) 386.4 IN/SEC^2 BASE INPUT
!!!!Pre-processing!!!!
RECTNG,20,,.25,, 
ET,1,PLANE42
UIMP,1,EX, , ,30e6, 
UIMP,1,NUXY, , ,.3, 
UIMP,1,DENS, , ,.000729,
LESIZE,2, , ,4, , , , ,1 
LESIZE,4, , ,4, , , , ,1  
LESIZE,1, , ,80, , , , ,1 
LESIZE,3, , ,80, , , , ,1 
MSHAPE,0,2D 
MSHKEY,1
AMESH,all   
nsel,s,loc,x,-10,.0001  
d,all,all,0
allsel,all
FINISH  
!!!!Solution Processing Modal Analysis!!!!
/SOLU   
ANTYPE,2
MODOPT,LANB,6   
EQSLV,SPAR  
MXPAND,6, , ,0  
MODOPT,LANB,6,0,0, ,OFF, ,0 
SOLVE   
FINISH  
!!!!Postprocess Modal Analysis!!!!
/POST1  
*GET,freq1,MODE,1,FREQ  
*GET,freq2,MODE,2,FREQ
*GET,freq3,MODE,3,FREQ
finish
!!!!Solution Process Full Harmonic Analysis!!!!
/solu
ANTYPE,3
HROPT,FULL  
HROUT,ON 
!*  
EQSLV,FRONT,0,  
DDELE,1,ALL  
nsel,s,loc,x,-10,.0001  
d,all,ux,0
d,all,uy,1
allsel,all
HARFRQ,freq2*.5,freq3*1.2, 
NSUBST,50, 
KBC,1   
!*  
DMPRAT,.0003, 
!*  
outres,all,all  
solve
!!!!Postprocess Harmonic Analysis!!!!
finish

/POST26 

NSOL,2,83,U,Y,                     ! raw results

PROD,3,1,1, , , , ,2*pi,2*pi,1,    ! omega squared in radians

QUOT,4,2,3, , , , ,xa,1,           ! displacements: (raw results*accel)/omega^2

PROD,5,2, , , , , ,xa              ! accelerations: raw results*accel

PROD,6,2, , , , , ,xa/g            ! g's: (raw results*accel)/g




