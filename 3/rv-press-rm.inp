/com,COLLABORATIVE SOLUTIONS, INC. (CSI) DOES NOT GUARANTEE THE CORRECTNESS
/com,OR USEFULNESS OF THE RESULTS OBTAINED USING THE EXAMPLE. CSI IS NOT
/com,LIABLE FOR ANY CONCLUSIONS OR ACTIONS BASED ON THE RESULTS.
/com,IT IS THE RESPONSIBILITY OF THE USER TO CONFIRM THE ACCURACY AND
/com,USEFULNESS OF THE RESULTS.
/com,
!com,AUTHOR:
!com,David Haberman
!com,Collaborative Solutions, Inc.
!com,5962 La Place Court Suite 100
!com,Carlsbad CA 92009
!com,760-431-4815
!com,david.haberman@csi-cae.com
/com,
/com,DESCRIPTION 
/com,Example of a random vibration analysis in ANSYS
/com,Pressure loads
/com,Reduced modal analysis
/com,

finish
/clear
!!!!Example of random vibrations element loads reduced modal analysis!!!!
!!!!Build Model!!!!
/PREP7  
RECTNG,10,,1,,  
ET,1,SHELL181   
KEYOPT,1,3,2  ! full intergration
R,1,.1, , , , , ,  
RMORE, , , , , , ,  
/MPLIB,READ,d:\apps\ansys56\matlib  
/MPLIB,WRITE,    
MAT,1,  
! /INPUT,d:\apps\ansys56\matlib\Stl_AISI-304.BIN_MPL
! /COM,Typical material properties for DEMO purposes only 
! /COM,ANSYS REVISION  5.3    UP042296 TEST    16:35:50    04/23/1996 
/NOP
! /COM,Internal UNITS set at file creation time = BIN 
TBDEL,ALL,_MATL 
MPDEL,ALL,_MATL 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,EX  ,_MATL , 1,  27992720.0    , 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,NUXY,_MATL , 1, 0.290000000    , 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,ALPX,_MATL , 1, 9.888888889E-06, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,DENS,_MATL , 1, 7.514795200E-04, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,KXX ,_MATL , 1, 2.182244000E-04, 
MPTEMP,R5.0, 1, 1, 0.000000000E+00, 
MPDATA,R5.0, 1,C   ,_MATL , 1,  46.2864080    , 
!
esize,.5   
amesh,all   
FINISH  
!!!!Modal Analysis!!!!
/SOLU   
ANTYPE,2
MODOPT,REDUC,10,0,0,0,OFF   
MXPAND,10, , ,1 
DL,4, ,ALL, 
!!!!Element loading!!!!
nsel,s,loc,x,8,10
sf,all,press,.005
m,all,all
allsel,all
TOTAL,100,0
SOLVE 
finish  
!!!!!!!PSD Analysis!!!!!!!!!!!!!!!!!!!!!!!!!!!
/sol
anty,spec
spopt,psd,,on
psdu,1,pres

psdf,1,,2,10,50,200,400
! Units (lbs/in^2)^2/Hz
psdv,1,2,8,8,5,2
dmpr,.0025
!!!!element loads!!!!
lvscale,1
!!!!!!!!!!!!!!!!!!!!!
pfac,1,node
psdcom,.001,10
psdr,disp,rel ! calculate disp results
psdr,velo,rel ! calculate velocity results
psdr,acel,rel ! calculate acceleration results
save
solv
fini
!!!!!!!!Post Process Post26 !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
/POST26 
numvar,50
STORE,PSD,10,  
!*
nsel,s,loc,x,10,10
nsel,r,loc,y,0,0
*get,xnd,node,,num,max
NSOL,2,xnd,U,Z,   
!*  
RPSD,3,2,,1,2,  ! displacement
RPSD,4,2,,2,2,  ! velocity
RPSD,5,2,,3,2,  ! acceleration
!
! Intergrate rspd over freq.
INT1,6,3,1, , , , ,1,1, ,   
INT1,7,4,1, , , , ,1,1, ,   
INT1,8,5,1, , , , ,1,1, ,   
!
! Get the area under the rpsd curve (intergration)
!
*get,uz6a,vari,6,extrem,vlast
*get,vz6a,vari,7,extrem,vlast
*get,az6a,vari,8,extrem,vlast
!
! Calculate the 1 sigma results by taking the 
! square root of the area under the rpsd curve
!
uz6b=sqrt(uz6a)  ! 1 sigma displacements
vz6b=sqrt(vz6a)  ! 1 sigma velocities
az6b=sqrt(az6a)  ! 1 sigma accelerations
!
!
!!!!!!!Post Process Post1 !!!!!!!!!!!!!!!!!!!
finish
/post1
set,3,1,
*GET,uz6,NODE,xnd,U,Z ! 1 sigma displacements
set,4,1
*get,vz6,node,xnd,u,z ! 1 sigma velocities
set,5,1
*get,az6,node,xnd,u,z ! 1 sigma accelerations
finish
!
! compare the following 1 sigma results
!
! post1     post26
! uz6   =   uz6b
! vz6   =   vz6b
! az6   =   az6b
!
