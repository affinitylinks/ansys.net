/BATCH
/config,nres,8000
/COM,ANSYS RELEASE  5.7 with Anand's model 

!**************************************
! Polymer: Underfill B
!**************************************

/GRA,POWER
/GST,ON
/PLO,INFO,3
/COL,PBAK,ON,1,BLUE 
/filname,CURE
/TITLE,Curing Process Simulation
!*  
/PREP7  

!************* Element types******************
et,1,plane183,,,2
et,2,visco108,,,2,,2,1
et,3,visco88,,,2  


!******** Material Properties ***************

!Temperature in K, Stresses in Pa, CTE in 1/K, Time in minutes 

toffst=0
tref=313

!Silicon material properties -- #1
mp,EX,1,106.9e9
mp,alpx,1,2.6e-6
mp,nuxy,1,0.25
mp,dens,1,1

!Solder material properties -- #2
mp,nuxy,2,0.40
mp,alpx,2,25.0e-6
tb,anand,2
tbdata,1,12.41e6,9400,1.44e10,1.5,0.303,1.379e9
tbdata,7,1.379e7,0.07,1.3

mptemp,1,200,450
mpdata,EX,2,1,4.55e10,0.758e10
mptemp
mp,dens,2,1

!FR-4 board material properties -- #3
mp,EX,3,18.6e9
mp,alpx,3,17.0e-6
mp,nuxy,3,0.20
mp,dens,3,1

!Underfill properties (not cured, 5%) -- #10
TB,EVISC,10
TBDATA,1,1.7264E4
TBDATA,2,0.5
TBDATA,3,1
TBDATA,6,1.0
TBDATA,26,78.0E-6
TBDATA,31,27.0E-6
TBDATA,36,273
TBDATA,46,0.2875e9
TBDATA,47,0.01e9
TBDATA,48,0.54E9
TBDATA,49,0.54E9
TBDATA,50,4
TBDATA,51,0.2870
TBDATA,52,0.3362
TBDATA,53,0.1623
TBDATA,54,0.2145
TBDATA,61,1E-14
TBDATA,62,1E-14
TBDATA,63,1E-14
TBDATA,64,1E-14

mp,dens,10,1


!************* Geometry Model Generation **********

nyl=4                      
nxl=1
tsub=1.0e-3
sbh=150e-6
tdie=0.75e-3

dw=4.323e-3
sw=dw+2.5*tsub
sbw=58.0e-6
smw=86.0e-6
scc=dw-367e-6

pitch=344.0e-6

*dim,ylayer,array,nyl
*dim,xlayer,array,nxl

ylayer(1)=0.0
ylayer(2)=tsub
ylayer(3)=tsub+sbh
ylayer(4)=tsub+sbh+tdie

xlayer(1)=0.0

*do,j,1,nxl
 *do,i,1,nyl
  k,,xlayer(j),ylayer(i)
 *enddo
*enddo

k,5,sw,ylayer(1)
k,6,sw,ylayer(2)
k,7,dw,ylayer(3)
k,8,dw,ylayer(4)

!*** substrate areas 
a,1,5,6,2


!*** silicon areas 
a,3,7,8,4

!*** solder joint area
k,101,scc-sbw,ylayer(2)
k,102,scc+sbw,ylayer(2)
k,103,scc+smw,(ylayer(2)+ylayer(3))/2
k,104,scc+sbw,ylayer(3)
k,105,scc-sbw,ylayer(3)
k,106,scc-smw,(ylayer(2)+ylayer(3))/2

l,101,102
spline,102,103,104
l,105,104
spline,105,106,101

al,9,10,11,12,13,14

agen,12,3,,,-1.*pitch

!*** underfill area
l,10,101
l,12,105
al,81,14,13,82,17,16
agen,11,15,,,-1.*pitch

l,2,3
l,2,69
l,3,73
al,143,144,80,79,145

k,,dw,ylayer(2)
l,141,102
l,7,141
l,7,104
al,146,147,148,11,10

k,,dw+sbh+tdie,ylayer(2)
l,141,8
l,141,142
l,142,8
al,149,150,151

!***
asel,all

aglue,all


!************* Meshing operations ************

!Solder area
*do,i,0,11
  lccat,10+i*6,11+i*6
  lccat,13+i*6,14+i*6
*enddo
asel,s,area,,3,14
aatt,2,,2

*do,i,0,11
  lesize,9+i*6,,,4
  lesize,12+i*6,,,4
*enddo

*do,i,0,11
lesize,10+i*6,,,4,3.5
lesize,11+i*6,,,4,0.286
lesize,13+i*6,,,4,3.5
lesize,14+i*6,,,4,0.286
*enddo

amesh,all

!*** Underfill area
asel,s,area,,15
aatt,10,,3
lesize,81,(pitch-2*sbw)/4
lesize,82,(pitch-2*sbw)/4
amesh,all

asel,s,area,,30,39
aatt,10,,3
*do,i,152,171
lesize,i,(pitch-2*sbw)/4
*enddo
amesh,all

asel,s,area,,26
aatt,10,,3
lesize,144,(pitch-2*sbw)/5
lesize,145,(pitch-2*sbw)/5
lesize,143,sbh/4
amesh,all

asel,s,area,,27
aatt,10,,3
lesize,146,(pitch-2*sbw)/4
lesize,148,(pitch-2*sbw)/4
lesize,147,sbh/4
amesh,all


!substrate areas
asel,s,area,,41
aatt,3,,1
esize,600e-6
!eshape,3,0
amesh,all

!die areas
asel,s,area,,40
aatt,1,,1
esize,430e-6
amesh,all

!Underfill fillet
asel,s,area,,29
aatt,10,,3
esize,380e-6
amesh,all


!******** Applye boundary conditions *********
nsel,s,loc,x,0
dsym,symm,x,0
nsel,r,loc,y,0
d,all,uy,0
nsel,all


!******** Group elements into components *****
esel,s,mat,,2
cm,solder,elem
esel,s,mat,,3
cm,board,elem
esel,s,mat,,1
cm,die,elem
esel,s,elem,,29,32
cm,intface,elem
esel,all


!******** Solve *********************
/solu

outres,all,last
antype,static,new
nlgeom,on
solcon,on,off
autots,off
ncnv,0
neqit,30

tref,313
toffst,0


!******* Parameters ****************
CTEr=78.e-6
CTEg=27.e-6
curetemp=438
t10=20.70
t30=21.30
t50=23.00
t70=25.00
t100=34.50
thold=30-(34.50-25.00)

shrink=-0.005
startemp=313
ramprate=5
reftemp=313
tcool=5
!tramp=(curetemp-startemp)/ramprate

temp10=ramprate*t10+startemp
temp30=ramprate*t30+startemp
temp50=ramprate*t50+startemp
temp70=ramprate*t70+startemp

temp100=curetemp
 

!******** Set initial condition *********
deltim,1
tunif,startemp
time,1e-5
solve

!******** Curing process ****************

!** ramp to the time of 10% curing degree
!** 5% curing degree property used

kbc,0
tunif,temp10
time,t10
save
solve
finish

!** ramp to the time of 30% curing degree
!** 20% curing degree property used

/delete,CURE,rdb
/delete,CURE,r001
/delete,CURE,ldhi

/filname,CURE
resume
/solu
antype,,rest


CTEeffR=CTEr+0.20*shrink/(temp30-reftemp)
CTEeffG=CTEg+0.20*shrink/(temp30-reftemp)

TB,EVISC,10
TBDATA,26,CTEeffR
TBDATA,31,CTEeffg
TBDATA,36,301
TBDATA,46,1.15E9
TBDATA,47,0.05E9
TBDATA,48,0.54E9
TBDATA,49,0.54E9
TBDATA,50,4
TBDATA,51,0.2870
TBDATA,52,0.3362
TBDATA,53,0.1623
TBDATA,54,0.2145
TBDATA,61,1.03E1
TBDATA,62,0.224E4
TBDATA,63,0.634E-3
TBDATA,64,1.04E-10

!save,CURE02,db
!finish

!/assign,rst,CURE02,rst
!/solu
!resume,CURE02,db

!deltim,0.1

tunif,temp30
!time,t30-t10
time,t30
save
solve
finish

!** ramp to the time of 50% curing degree
!** 40% curing degree property used

/delete,CURE,r001
/delete,CURE,ldhi

/filname,CURE
resume
/solu
antype,,rest

CTEeffR=CTEr+0.40*shrink/(temp50-reftemp)
CTEeffG=CTEg+0.40*shrink/(temp50-reftemp)

TB,EVISC,10
TBDATA,26,CTEeffR
TBDATA,31,CTEeffg
TBDATA,36,329
TBDATA,46,2.3E9
TBDATA,47,0.10E9
TBDATA,48,0.54E9
TBDATA,49,0.54E9
TBDATA,50,4
TBDATA,51,0.2870
TBDATA,52,0.3362
TBDATA,53,0.1623
TBDATA,54,0.2145
TBDATA,61,2.06E1
TBDATA,62,0.448E4
TBDATA,63,1.27E-3
TBDATA,64,2.08E-10

!save,CURE04,db
!finish

!/assign,rst,CURE04,rst
!/solu
!resume,CURE04,db
tunif,temp50
!time,t50-t30
time,t50
save
solve
finish

!** ramp to the time of 70% curing degree
!** 60% curing degree property used

/delete,CURE,r001
/delete,CURE,ldhi

/filname,CURE
resume
/solu
antype,,rest

CTEeffR=CTEr+0.60*shrink/(temp70-reftemp)
CTEeffG=CTEg+0.60*shrink/(temp70-reftemp)

TB,EVISC,10
TBDATA,26,CTEeffR
TBDATA,31,CTEeffg
TBDATA,36,357
TBDATA,46,3.45E9
TBDATA,47,0.15E9
TBDATA,48,0.54E9
TBDATA,49,0.54E9
TBDATA,50,4
TBDATA,51,0.2870
TBDATA,52,0.3362
TBDATA,53,0.1623
TBDATA,54,0.2145
TBDATA,61,3.08E1
TBDATA,62,0.672E4
TBDATA,63,1.90E-3
TBDATA,64,3.13E-10

!save,CURE06,db
!finish

!/assign,rst,CURE06,rst
!/solu
!resume,CURE06,db
tunif,temp70
!time,t70-t50
time,t70
save
solve
finish

!** ramp to the time of 100% curing degree
!** 80% curing degree property used

/delete,CURE,r001
/delete,CURE,ldhi

/filname,CURE
resume
/solu
antype,,rest

CTEeffR=CTEr+0.80*shrink/(temp100-reftemp)
CTEeffG=CTEg+0.80*shrink/(temp100-reftemp)

TB,EVISC,10
TBDATA,26,CTEeffR
TBDATA,31,CTEeffg
TBDATA,36,385
TBDATA,46,4.60E9
TBDATA,47,0.20E9
TBDATA,48,0.54E9
TBDATA,49,0.54E9
TBDATA,50,4
TBDATA,51,0.2870
TBDATA,52,0.3362
TBDATA,53,0.1623
TBDATA,54,0.2145
TBDATA,61,4.11E1
TBDATA,62,0.896E4
TBDATA,63,2.54E-3
TBDATA,64,4.17E-10


!save,CURE08,db
!finish

!/assign,rst,CURE08,rst
!/solu
!resume,CURE08,db
tunif,temp100
!time,t100-t70
time,t100
save
solve
finish


!** Hold time
!** 100% curing degree property used

/delete,CURE,r001
/delete,CURE,ldhi

/filname,CURE
resume
/solu
antype,,rest

CTEeffR=CTEr+1.00*shrink/(curetemp-reftemp)
CTEeffG=CTEg+1.00*shrink/(curetemp-reftemp)

TB,EVISC,10
TBDATA,26,CTEeffR
TBDATA,31,CTEeffg
TBDATA,36,413
TBDATA,46,5.75E9
TBDATA,47,0.25E9
TBDATA,48,0.54E9
TBDATA,49,0.54E9
TBDATA,50,4
TBDATA,51,0.2870
TBDATA,52,0.3362
TBDATA,53,0.1623
TBDATA,54,0.2145
TBDATA,61,5.14E1
TBDATA,62,1.12E4
TBDATA,63,3.17E-3
TBDATA,64,5.21E-10

kbc,1

!save,CURE10,db
!finish

!/assign,rst,CURE10,rst
!/solu
!resume,CURE10,db
tunif,curetemp
!time,tramp-t100
time,t100+thold
save
solve
finish


!** Cool down

/delete,CURE,r001
/delete,CURE,ldhi

/filname,CURE
resume
/solu
antype,,rest

TB,EVISC,10
TBDATA,26,78.0E-6
TBDATA,31,27.0E-6
TBDATA,36,413
TBDATA,46,5.75E9
TBDATA,47,0.25E9
TBDATA,48,0.54E9
TBDATA,49,0.54E9
TBDATA,50,4
TBDATA,51,0.2870
TBDATA,52,0.3362
TBDATA,53,0.1623
TBDATA,54,0.2145
TBDATA,61,5.14E1
TBDATA,62,1.12E4
TBDATA,63,3.17E-3
TBDATA,64,5.21E-10

kbc,0
tunif,startemp
time,t100+thold+tcool
solve
save
finish

!********** Post-processing ***********

/post1

PLESOL,NL,PLWK,0,1

esel,all

finish

!******

/eof
